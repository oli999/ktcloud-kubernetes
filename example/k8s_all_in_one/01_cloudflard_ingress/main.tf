# main.tf
terraform {
  required_providers {
    # cloudflare 를 terraform 에서 사용할수 있도록 준비
    cloudflare = {
      source  = "cloudflare/cloudflare"
      version = "~> 4.0"
    }
    random = {
      source  = "hashicorp/random"
      version = "~> 3.0"
    }
    # terraform 으로 k8s 자원들을 provision 할수 있도록 provider 추가 
    kubernetes = {
      source  = "hashicorp/kubernetes"
      version = "~> 2.30" 
    }
    # terraform 으로 helm chart 를 직접 배포 가능하도록 하는 provider 추가
    helm = {
      source = "hashicorp/helm"
      version = "~> 2.14"
    }    
  }
}

# 클러스터 접속정보 (eks 를 바라 보도록 context 가 변경되어 있어야 한다)
provider "kubernetes" {
  config_path = "~/.kube/config"
}

# helm provider 가 동작하려면 config 파일 정보를 전달해야 한다. 
provider "helm" {
  kubernetes {
    config_path = "~/.kube/config"
  }
}

# cloudflared 에 로그인해서 아래의 변수에 전달할 정보를 가지고 와서 terraform.tfvars 파일에 미리 기입을 해둔다.
variable "cloudflare_api_token" {
  description = "Cloudflare API Token"
  type        = string
  sensitive   = true
}

variable "cloudflare_zone_id" {
  description = "Cloudflare Zone ID (도메인의 고유 ID)"
  type        = string
}

variable "cloudflare_account_id" {
  description = "Cloudflare account ID"
  type        = string
}

variable "domain_name" {
  description = "연결할 외부 도메인 (예: yourdomain.com)"
  type        = string
}

# --- Provider -----------------------------------------------------------------
provider "cloudflare" {
  # 변수에 있는 api 토큰을 사용한다 
  api_token = var.cloudflare_api_token
}

# --- 1. Tunnel Secret 생성 (터널 인증용 무작위 암호) ------------------------
resource "random_password" "tunnel_secret" {
  length  = 64
  special = false
}

# --- 2. Cloudflare Tunnel 본체 생성 -------------------------------------------
resource "cloudflare_tunnel" "vmware_tunnel" {
  account_id = var.cloudflare_account_id
  name       = "vmware-local-tunnel2"
  secret     = base64encode(random_password.tunnel_secret.result)
}

# --- 3. DNS CNAME 레코드 생성 (내 도메인 -> 클라우드플레어 터널 연결) ---
resource "cloudflare_record" "vmware_dns" {
  # 1. Zone ID: 어떤 도메인(예: cloud-learning.site)에 레코드를 추가할지 지정합니다. (변수에서 가져옴)
  zone_id = var.cloudflare_zone_id

  # 2. 레코드 이름(Name): "@"는 서브도메인(www 등) 없이 '루트 도메인' 자체로 접속함을 의미합니다.
  name    = "@"

  # 3. 목적지(Content): 도메인으로 들어온 트래픽을 보낼 도착지입니다. 
  # 클라우드플레어가 발급한 "터널의 고유 ID.cfargotunnel.com" 주소로 동적 라우팅합니다.
  content = "${cloudflare_tunnel.vmware_tunnel.id}.cfargotunnel.com"

  # 4. 레코드 타입(Type): IP 주소가 아닌 도메인 이름(cfargotunnel.com)으로 연결하므로 'CNAME'을 사용합니다.
  type    = "CNAME"

  # 5. 프록시 활성화 (proxied = true): 클라우드플레어의 핵심 기능입니다. (주황색 구름 아이콘 ON)
  # 이 옵션이 true여야 무료 SSL(HTTPS) 인증서, DDoS 공격 방어, CDN 캐싱 기능이 터널에 적용됩니다.
  proxied = true
}


# --- 4. Tunnel 라우팅 규칙 (리버스 프록시 설정) -------------------------------
# 터널로 들어온 트래픽을 사설망 어디로 보낼지 결정합니다.
resource "cloudflare_tunnel_config" "vmware_config" {
  account_id = cloudflare_tunnel.vmware_tunnel.account_id
  tunnel_id  = cloudflare_tunnel.vmware_tunnel.id

  config {
    # 첫 번째 규칙: 지정한 도메인으로 들어오면 vmware 내부 서비스로 전달
    ingress_rule {
      # cloud-learning.site 로 요청이 들어오면 터널과 연결된 아래의 위치로 전달한다 
      hostname = var.domain_name
      # 인그래스 컨트롤러로 전달
      service  = "http://ingress-nginx-controller.ingress-nginx.svc.cluster.local:80" 

      # local cluster 의 svc 중에서 default 네임스페이스에 있는 nginx-svc 라는 이름의 서비스로 전달
      # service  = "http://nginx-svc.default.svc.cluster.local:80"
    }
    
    # 마지막 규칙: 매칭되는 도메인이 없으면 404 에러 반환 (필수 설정)
    ingress_rule {
      service = "http_status:404"
    }
  }
}


resource "null_resource" "install_cloudflared_pod" {
    # 1. 터널 라우팅 규칙과 DNS 세팅이 "완벽히 끝난 후"에 앤서블을 실행해라!
    depends_on = [
        cloudflare_tunnel_config.vmware_config,
        cloudflare_record.vmware_dns
    ]

    # 2. 터널 토큰이 바뀌면 무조건 앤서블을 다시 실행해라! (항상 실행하려면 timestamp 적용)
    triggers = {
        always_run = "${timestamp()}"
    }

    # aws 에 프로비저닝을 하는것이 아닌 local 에서 직접 ansible 플레이북을 실행 
    provisioner "local-exec" {
        command = "ansible-playbook -i localhost, -c local playbook-kubectl.yml --extra-vars 'tunnel_token=${cloudflare_tunnel.vmware_tunnel.tunnel_token}'"
    }

    # terraform destroy 했을때 deploy 와 secret 도 같이 삭제 되도록 한다.
    provisioner "local-exec"{
      when = destroy
      command = <<-EOF
        kubectl delete -f deploy-cloudflared.yaml
        kubectl delete secret tunnel-credentials -n k8s --ignore-not-found=true
      EOF
    }
}

# 터널 토큰을 얻어내기 위한 블럭 
output "tunnel_real_token" {
  description = "Cloudflare Tunnel Token"
  value       = cloudflare_tunnel.vmware_tunnel.tunnel_token
  sensitive   = true  
}

resource "helm_release" "ingress_nginx" {
  name             = "ingress-nginx"
  # helm 저장소의 위치
  repository       = "https://kubernetes.github.io/ingress-nginx"
  # chart 의 이름
  chart            = "ingress-nginx"
  # chart 버전
  version          = "4.11.0"
  # namespace 설정 
  namespace        = "ingress-nginx"
  create_namespace = true
  # 서비스 type 을 LoadBalancer 가 아닌 ClusterIP type 으로 만들어 지도록 설정
  set {
    name  = "controller.service.type"
    value = "ClusterIP"
  }
}